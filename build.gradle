apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'

buildscript {
  apply from: "${rootDir}/gradle/dependencies.gradle"

  repositories {
    maven { url 'https://plugins.gradle.org/m2' }
    mavenCentral()
    jcenter()
  }

  dependencies {
    gradle_plugins.each { name, dependency -> classpath dependency }
  }
}

task testReport(type: TestReport, group: 'Build') {
  description = 'Generates an aggregate test report'
  destinationDir = file("${buildDir}/reports/allTests")
}

allprojects {
  apply from: "${rootDir}/gradle/eclipse.gradle"

  repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://jitpack.io' }
    mavenCentral()
    jcenter()
  }
}

subprojects {
  apply plugin: 'com.github.ethankhall.semantic-versioning'
  apply plugin: 'nebula.provided-base'
  apply plugin: 'java'
  apply plugin: 'osgi'

  apply from: "${rootDir}/gradle/publish.gradle"
  apply from: "${rootDir}/gradle/code_quality.gradle"
  apply from: "${rootDir}/gradle/dependencies.gradle"
  apply from: "${rootDir}/gradle/object_layout.gradle"

  if (JavaVersion.current().isJava9Compatible()) {
    tasks.uploadArchives.enabled = false
  } else {
    apply plugin: 'net.ltgt.errorprone'  
  }

  sourceCompatibility = JavaVersion.VERSION_1_8

  group = 'com.github.ben-manes.caffeine'
  version.with {
    major = 2 // incompatible API changes
    minor = 3 // backwards-compatible additions
    patch = 4 // backwards-compatible bug fixes
    releaseBuild = rootProject.hasProperty('release')
  }
  archivesBaseName = path[1..-1].replaceAll(':', '-').toLowerCase()

  dependencies {
    provided libraries.jsr305
    provided libraries.error_prone_annotations

    testCompile libraries.guava
    testCompile test_libraries.mockito
    testCompile test_libraries.hamcrest
    testCompile test_libraries.awaitility
    testCompile test_libraries.osgi_compile

    testRuntime test_libraries.osgi_runtime
  }

  configurations {
    testArtifacts
  }

  tasks.withType(Test) {
    if (!it.name.startsWith('slow')) {
      rootProject.testReport.reportOn it
    }
    it.dependsOn('jar')

    // ensure tasks don't overwrite the default report directories used by the 'test' task
    reports.html.destination = "${buildDir}/reports/${name}"
    reports.junitXml.destination = "${buildDir}/reports/${name}/results"
    binResultsDir = file("${buildDir}/reports/${name}/results/binary/${name}")
  }

  task testJar(type: Jar, group: 'Build') {
    description = 'Assembles a jar archive containing the test classes.'
    baseName = "${archivesBaseName}-test"
    from sourceSets.test.output
  }

  artifacts {
    testArtifacts testJar
  }

  if (project != project(':caffeine')) {
    javadoc.options.linksOffline(
      "http://static.javadoc.io/${group}/caffeine/${version}/",
      "${project(':caffeine').buildDir}/docs/javadoc/",
    )
    javadoc.dependsOn(project(':caffeine').javadoc)
  }
}

// Only report code coverage for projects that are distributed
def publishedProjects = subprojects.findAll { it.path != ':simulator' }

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
  description = 'Generates an aggregate report from all subprojects'
  dependsOn(publishedProjects.test)

  additionalSourceDirs = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  sourceDirectories = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  classDirectories = files(publishedProjects.sourceSets.main.output)
  executionData = files(publishedProjects.jacocoTestReport.executionData)

  reports {
    html.enabled = true // human readable
    xml.enabled = true // required by coveralls
  }

  doFirst {
    executionData = files(executionData.findAll { it.exists() })
  }
}

coveralls {
  sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
  jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
  group = 'Coverage reports'
  description = 'Uploads the aggregated coverage report to Coveralls'

  dependsOn jacocoRootReport
  onlyIf { System.env.'CI' && !JavaVersion.current().isJava9Compatible() }
}
