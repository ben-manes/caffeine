import org.gradle.plugins.ide.eclipse.model.SourceFolder

apply from: "${rootDir}/gradle/jmh.gradle"
apply from: "${projectDir}/testing.gradle"

sourceSets {
  javaPoet {
    java.srcDir 'src/javaPoet/java'
  }
  main {
    java.srcDir "${buildDir}/generated-sources/"
  }
  codeGen {
    java.srcDir "${buildDir}/generated-sources/"
  }
}

configurations {
  javaAgent
}

idea.module {
  scopes.PROVIDED.plus += [ configurations.javaPoetCompileClasspath ]
}

eclipse.classpath.file.whenMerged {
  entries.findAll { it instanceof SourceFolder && it.output == 'bin/codeGen' }*.output = 'bin/main'
}

plugins.withType(EclipsePlugin) {
  project.eclipse.classpath.plusConfigurations += [ configurations.javaPoetCompileClasspath ]
}

dependencies {
  api libraries.checkerAnnotations
  api libraries.errorproneAnnotations

  testImplementation libraries.joor
  testImplementation libraries.ycsb
  testImplementation libraries.guava
  testImplementation libraries.fastutil
  testImplementation testLibraries.junit
  testImplementation testLibraries.testng
  testImplementation testLibraries.jctools
  testImplementation libraries.commonsLang3
  testImplementation testLibraries.guavaTestLib

  javaAgent libraries.jamm

  jmh libraries.jamm
  jmh libraries.guava
  jmh libraries.tcache
  jmh libraries.cache2k
  jmh libraries.ehcache3
  jmh libraries.fastutil
  jmh libraries.slf4jNop
  jmh libraries.flipTables
  jmh libraries.jackrabbit
  jmh libraries.expiringMap
  jmh libraries.elasticSearch
  jmh libraries.concurrentlinkedhashmap

  javaPoetImplementation libraries.guava
  javaPoetImplementation libraries.javapoet
  javaPoetImplementation libraries.commonsLang3
  javaPoetImplementation libraries.googleJavaFormat
}

compileCodeGenJava {
  gradle.taskGraph.whenReady {
    enabled = gradle.taskGraph.hasTask('uploadArchives')
  }
  dependsOn compileJava
  destinationDir = compileJava.destinationDir
  classpath = sourceSets.main.runtimeClasspath + sourceSets.main.output
  options.incremental = false
  options.debug = false
}

jar.manifest {
  attributes 'Bundle-SymbolicName': 'com.github.ben-manes.caffeine'
  attributes 'Import-Package': ''
  attributes 'Export-Package': [
    'com.github.benmanes.caffeine',
    'com.github.benmanes.caffeine.base',
    'com.github.benmanes.caffeine.cache',
    'com.github.benmanes.caffeine.cache.stats'].join(',')
  attributes 'Automatic-Module-Name': 'com.github.benmanes.caffeine'
}

jar.dependsOn(compileCodeGenJava)

sonarqube {
  properties {
    property "sonar.exclusions", '**/NodeFactory.java, **/LocalCacheFactory.java'
  }
}

task generateLocalCaches(type: JavaExec) {
  main = 'com.github.benmanes.caffeine.cache.LocalCacheFactoryGenerator'
  classpath = sourceSets.javaPoet.runtimeClasspath
  args "${buildDir}/generated-sources/"

  outputs.upToDateWhen { !tasks.compileJavaPoetJava.didWork }
  outputs.dir "${buildDir}/generated-sources/"
  outputs.cacheIf { true }
}
compileJava.dependsOn(generateLocalCaches)
sourcesJar.dependsOn(generateLocalCaches)

task generateNodes(type: JavaExec) {
  main = 'com.github.benmanes.caffeine.cache.NodeFactoryGenerator'
  classpath = sourceSets.javaPoet.runtimeClasspath
  args "${buildDir}/generated-sources/"

  outputs.upToDateWhen { !tasks.compileJavaPoetJava.didWork }
  outputs.dir "${buildDir}/generated-sources/"
  outputs.cacheIf { true }
}
compileJava.dependsOn(generateNodes)
sourcesJar.dependsOn(generateNodes)

task memoryOverhead(type: JavaExec, group: 'Benchmarks', description: 'Evaluates cache overhead') {
  classpath sourceSets.jmh.runtimeClasspath
  jvmArgs "-javaagent:${configurations.javaAgent.singleFile}"
  main = 'com.github.benmanes.caffeine.cache.MemoryBenchmark'
}
