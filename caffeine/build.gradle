apply from: "${rootDir}/gradle/jmh.gradle"
apply from: "${projectDir}/testing.gradle"
  
sourceSets {
  javaPoet {
    java.srcDir 'src/javaPoet/java'
  }
  main {
    java.srcDir "${buildDir}/generated-sources/"
  }
}

configurations {
  javaAgent
}

plugins.withType(EclipsePlugin) {
  project.eclipse.classpath.plusConfigurations += [ configurations.javaPoetCompile ]
}

dependencies {
  compile project(':tracing:api')

  testCompile project(':simulator')
  testCompile project(path: ':tracing:api', configuration: 'testArtifacts')
  testCompile libraries.guava
  testCompile test_libraries.awaitility
  testCompile test_libraries.guava_testlib
  testCompile test_libraries.jctools
  testCompile test_libraries.mockito
  testCompile test_libraries.testng

  javaAgent benchmark_libraries.jamm

  jmh libraries.flip_tables
  jmh benchmark_libraries.jamm
  jmh benchmark_libraries.cache2k
  jmh benchmark_libraries.ehcache2
  jmh benchmark_libraries.ehcache3
  jmh benchmark_libraries.koloboke
  jmh benchmark_libraries.slf4j_nop
  jmh benchmark_libraries.infinispan
  jmh benchmark_libraries.high_scale_lib
  jmh benchmark_libraries.concurrentlinkedhashmap
  
  javaPoetCompile libraries.guava
  javaPoetCompile libraries.jsr305
  javaPoetCompile libraries.javapoet
}

bundle {
  instruction 'Import-Package', 'sun.misc.*;resolution:=optional'
  instruction 'Export-Package', [
    'com.github.benmanes.caffeine',
    'com.github.benmanes.caffeine.base',
    'com.github.benmanes.caffeine.cache',
    'com.github.benmanes.caffeine.cache.stats',
    'com.github.benmanes.caffeine.locks',
  ].join(';')
}

task generateLocalCaches(type: JavaExec) {
  main = 'com.github.benmanes.caffeine.cache.LocalCacheFactoryGenerator'
  classpath = sourceSets.javaPoet.runtimeClasspath
  args "${buildDir}/generated-sources/"
  jvmArgs += '-noverify'

  outputs.upToDateWhen { !tasks.compileJavaPoetJava.didWork }
}
compileJava.dependsOn(generateLocalCaches)

task generateNodes(type: JavaExec) {
  main = 'com.github.benmanes.caffeine.cache.NodeFactoryGenerator'
  classpath = sourceSets.javaPoet.runtimeClasspath
  args "${buildDir}/generated-sources/"
  jvmArgs += '-noverify'

  outputs.upToDateWhen { !tasks.compileJavaPoetJava.didWork }
}
compileJava.dependsOn(generateNodes)

task memoryOverhead(type: JavaExec, group: 'Benchmarks', description: 'Evaluates cache overhead') {
  classpath sourceSets.jmh.runtimeClasspath
  jvmArgs "-javaagent:${configurations.javaAgent.singleFile}"
  main = 'com.github.benmanes.caffeine.cache.MemoryBenchmark'
}
