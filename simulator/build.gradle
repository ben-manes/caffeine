/** Cache simulator using tracing data and a family of eviction policy options. */
apply plugin:'application'

mainClassName = 'com.github.benmanes.caffeine.cache.simulator.Simulator'

dependencies {
  compile project(':caffeine')

  compile libraries.xz
  compile libraries.akka
  compile libraries.ycsb
  compile libraries.guava
  compile libraries.stream
  compile libraries.fastutil
  compile libraries.flipTables
  compile benchmarkLibraries.ohc
  compile libraries.commonsLang3
  compile libraries.commonsCompress
  compile benchmarkLibraries.tcache
  compile libraries.univocityParsers
  compile benchmarkLibraries.cache2k
  compile benchmarkLibraries.ehcache2
  compile benchmarkLibraries.ehcache3
  compile benchmarkLibraries.rapidoid
  compile benchmarkLibraries.collision
  compile benchmarkLibraries.slf4jNop
  compile benchmarkLibraries.expiringMap
  compile benchmarkLibraries.elasticSearch

  testCompile testLibraries.testng
}

test {
  useTestNG()
}

coverity {
  skip = true
}

sonarqube {
  skipProject = true
}

jar.manifest {
  name 'com.github.benmanes.caffeine.simulator'
  instruction 'Automatic-Module-Name',
    'com.github.benmanes.caffeine.simulator'
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
  // https://github.com/akka/akka/issues/21165
  enabled = !JavaVersion.current().isJava9Compatible()
}

run {
  systemProperties System.properties.findAll {
    it.getKey().startsWith('akka') || it.getKey().startsWith('caffeine')
  }
}
