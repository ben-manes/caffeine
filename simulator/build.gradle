/** Cache simulator using tracing data and a family of eviction policy options. */
apply plugin:'application'

mainClassName = 'com.github.benmanes.caffeine.cache.simulator.Simulator'

dependencies {
  compile project(':caffeine')

  compile libraries.xz
  compile libraries.akka
  compile libraries.ycsb
  compile libraries.guava
  compile libraries.fastutil
  compile libraries.flipTables
  compile benchmarkLibraries.ohc
  compile libraries.commonsLang3
  compile libraries.commonsCompress
  compile benchmarkLibraries.tcache
  compile libraries.univocityParsers
  compile benchmarkLibraries.cache2k
  compile benchmarkLibraries.ehcache3
  compile benchmarkLibraries.collision
  compile benchmarkLibraries.slf4jNop
  compile benchmarkLibraries.expiringMap
  compile benchmarkLibraries.elasticSearch
  compile 'com.clearspring.analytics:stream:2.9.5'
  compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
  compile group: 'com.sangupta', name: 'murmur', version: '1.0.0' 

  testCompile testLibraries.testng
}

test {
  useTestNG()
}

// JMH zip archive overlaps with the application's
if (!(gradle.startParameter.taskNames ==~ /.*uploadArchives.*/)) {
  apply from: "${rootDir}/gradle/jmh.gradle"

  jmh {
    benchmarkMode = ['avgt']
    warmupIterations = 1
    iterations = 3
  }
}

coverity {
  skip = true
}

sonarqube {
  skipProject = true
}

jar.manifest {
  name 'com.github.benmanes.caffeine.simulator'
  instruction 'Automatic-Module-Name',
    'com.github.benmanes.caffeine.simulator'
}

compileJava.options.compilerArgs += [ '-Xep:NullAway:OFF' ]

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
  // https://github.com/akka/akka/issues/21165
  enabled = !JavaVersion.current().isJava9Compatible()
}

run {
  systemProperties System.properties.findAll {
    it.getKey().startsWith('akka') || it.getKey().startsWith('caffeine')
  }
}
