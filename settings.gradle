plugins {
  id 'com.gradle.enterprise' version '3.13'
  id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.10'
  id 'org.gradle.toolchains.foojay-resolver-convention' version '0.4.0'
}

gradleEnterprise {
  if (!gradle.startParameter.isBuildScan()) {
    server = 'https://caffeine.gradle-enterprise.cloud/'
    buildScan.publishIfAuthenticated()
  }
  buildScan {
    capture.taskInputFiles = true

    if (System.env.CI) {
      uploadInBackground = false
      publishAlways()
    } else {
      obfuscation.ipAddresses { [] }
    }
    if (System.env.GITHUB_ACTIONS) {
      obfuscation.username { 'github' }
    }

    termsOfServiceAgree = 'yes'
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
  }
}

def gradleEnterpriseCachePassword = properties.'gradleEnterpriseCachePassword'
buildCache {
  local.enabled = true
  remote(HttpBuildCache) {
    url = 'https://caffeine.gradle-enterprise.cloud/cache/'
    push = System.env.CI
    enabled = true
    credentials {
      username = 'ci'
      password = gradleEnterpriseCachePassword ?: System.env.GRADLE_ENTERPRISE_CACHE_PASSWORD
    }
  }
}

dependencyResolutionManagement.repositories {
  mavenCentral()
}

rootProject.name = 'caffeine'

include 'caffeine'
include 'guava'
include 'jcache'
include 'simulator'
