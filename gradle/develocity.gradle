develocity {
  if (!gradle.startParameter.isBuildScan()) {
    server = 'https://caffeine.gradle-enterprise.cloud/'
    buildScan.publishing.onlyIf { it.authenticated }
  }
  buildScan {
    if (System.env.CI) {
      uploadInBackground = false
    } else {
      obfuscation.ipAddresses { [] }
    }
    if (providers.environmentVariable('GITHUB_ACTIONS').isPresent()) {
      obfuscation.username { 'github' }
    }

    termsOfUseUrl = 'https://gradle.com/terms-of-service'
    termsOfUseAgree = 'yes'
  }
}

buildCache {
  def ci = providers.environmentVariable('CI')
  def overrideCi = providers.environmentVariable('LOCAL_BUILD_CACHE_CI')
  def hasAccessKey = providers.environmentVariable('DEVELOCITY_ACCESS_KEY')

  remote(develocity.buildCache) {
    // Check access key presence to avoid build cache errors on PR builds when not present
    push = ci.isPresent() && hasAccessKey.isPresent()
    enabled = !gradle.startParameter.isBuildScan()
  }
  local.enabled = !ci.isPresent() || overrideCi.map { it.toBoolean() }.getOrElse(false)
}
