/**
 * Defines dependencies and versions shared across modules.
 *
 * Multiple dependencies can be shared under the same name:
 *   spring: [
 *     "org.springframework:spring-core:${versions.spring}",
 *     "org.springframework:spring-jdbc:${versions.spring}"
 *   ]
 *
 * Use DependencyHandler.create for advanced dependency configuration:
 *   dependencies.create("org.springframework:spring-core:3.1") {
 *     // Force a version in case of the conflict
 *     force = true
 *
 *     // Disable all transitive dependencies
 *     transitive = false
 *
 *     // exclude a transitive dependency
 *     exclude module: "org.unwanted:unwantedModule" // by group & name
 *     exclude module: "unwantedModule"              // by artifact name
 *     exclude group:  "org.unwanted'                // by artifact group
 *   }
 *
 * @see https://gradle.org/docs/current/javadoc/org/gradle/api/artifacts/dsl/DependencyHandler.html
 */
ext {
  versions = [
    akka: '2.6.0-RC2',
    cache2k: '1.3.1.Alpha',
    checkerFramework: '3.0.0',
    collision: '0.3.3',
    commonsCompress: '1.19',
    commonsLang3: '3.9',
    commonsMath3: '3.6.1',
    concurrentlinkedhashmap: '1.4.2',
    config: '1.4.0',
    ehcache3: '3.8.1',
    errorprone: '2.3.3',
    errorproneJavac: '9+181-r4173-1',
    elasticSearch: '7.4.2',
    expiringMap: '0.5.9',
    fastfilter: 'bf0b02297f',
    fastutil: '8.3.0',
    flipTables: '1.0.2',
    guava: '28.1-jre',
    jackrabbit: '1.18.0',
    jamm: '0.3.3',
    javaObjectLayout: '0.9',
    javapoet: '1.11.1',
    jcache: '1.1.1',
    jcommander: '1.78',
    jmh: '1.22',
    jsr330: '1',
    koloboke: '0.6.8',
    nullaway: '0.7.8',
    ohc: '0.6.1',
    slf4j: '1.7.29',
    tcache: '2.0.1',
    stream: '2.9.8',
    univocityParsers: '2.8.3',
    ycsb: '0.17.0',
    xz: '1.8',
  ]
  testVersions = [
    awaitility: '4.0.1',
    easymock: '4.0.2',
    hamcrest: '2.2',
    jcacheTck: '1.1.1',
    jctools: '2.1.2',
    junit: '4.12',
    mockito: '3.1.0',
    paxExam: '4.13.1',
    testng: '7.0.0',
    truth: '0.24',
  ]
  pluginVersions = [
    apt: '0.21',
    bnd: '4.3.0',
    buildscan: '2.4.2',
    checkstyle: '8.26',
    coveralls: '2.8.3',
    coverity: '1.0.10',
    errorprone: '1.1.1',
    jacoco: '0.8.4',
    jmh: '0.5.0',
    jmhReport: '0.9.0',
    nexus: '2.3.1',
    nullaway: '1.0.1',
    pmd: '6.17.0',
    semanticVersioning: '1.1.0',
    shadow: '5.1.0',
    sonarqube: '2.8',
    spotbugs: '3.1.12',
    spotbugsPlugin: '2.0.0',
    stats: '0.2.2',
    versions: '0.27.0',
  ]

  libraries = [
    akka: "com.typesafe.akka:akka-actor_2.12:${versions.akka}",
    cache2k: "org.cache2k:cache2k-core:${versions.cache2k}",
    checkerAnnotations: "org.checkerframework:checker-qual:${versions.checkerFramework}",
    collision: "systems.comodal:collision:${versions.collision}",
    commonsCompress: "org.apache.commons:commons-compress:${versions.commonsCompress}",
    commonsLang3: "org.apache.commons:commons-lang3:${versions.commonsLang3}",
    commonsMath3: "org.apache.commons:commons-math3:${versions.commonsMath3}",
    concurrentlinkedhashmap: "com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:${versions.concurrentlinkedhashmap}",
    config: "com.typesafe:config:${versions.config}",
    ehcache3: "org.ehcache:ehcache:${versions.ehcache3}",
    errorproneAnnotations: "com.google.errorprone:error_prone_annotations:${versions.errorprone}",
    errorproneCore: "com.google.errorprone:error_prone_core:${versions.errorprone}",
    errorproneJavac: "com.google.errorprone:javac:${versions.errorproneJavac}",
    elasticSearch: dependencies.create("org.elasticsearch:elasticsearch:${versions.elasticSearch}") {
      exclude group: 'org.apache.lucene'
    },
    expiringMap: "net.jodah:expiringmap:${versions.expiringMap}",
    fastfilter: "com.github.FastFilter:fastfilter_java:${versions.fastfilter}",
    fastutil: "it.unimi.dsi:fastutil:${versions.fastutil}",
    flipTables: "com.jakewharton.fliptables:fliptables:${versions.flipTables}",
    guava: "com.google.guava:guava:${versions.guava}",
    jackrabbit: dependencies.create("org.apache.jackrabbit:oak-core:${versions.jackrabbit}") {
      exclude group: 'junit'
    },
    jamm: "com.github.jbellis:jamm:${versions.jamm}",
    javaObjectLayout: "org.openjdk.jol:jol-cli:${versions.javaObjectLayout}",
    javapoet: "com.squareup:javapoet:${versions.javapoet}",
    jcache: "javax.cache:cache-api:${versions.jcache}",
    jcommander: "com.beust:jcommander:${versions.jcommander}",
    jsr330: "javax.inject:javax.inject:${versions.jsr330}",
    koloboke: [
      "net.openhft:koloboke-api-jdk8:${versions.koloboke}",
      "net.openhft:koloboke-impl-jdk8:${versions.koloboke}",
    ],
    nullaway: "com.uber.nullaway:nullaway:${versions.nullaway}",
    ohc: "org.caffinitas.ohc:ohc-core-j8:${versions.ohc}",
    slf4jNop: "org.slf4j:slf4j-nop:${versions.slf4j}",
    tcache: "com.trivago:triava:${versions.tcache}",
    stream: "com.clearspring.analytics:stream:${versions.stream}",
    univocityParsers: "com.univocity:univocity-parsers:${versions.univocityParsers}",
    ycsb: "site.ycsb:core:${versions.ycsb}",
    xz: "org.tukaani:xz:${versions.xz}",
  ]
  testLibraries = [
    awaitility: "org.awaitility:awaitility:${testVersions.awaitility}",
    easymock: "org.easymock:easymock:${testVersions.easymock}",
    guavaTestLib: dependencies.create("com.google.guava:guava-testlib:${versions.guava}") {
      exclude group: 'com.google.truth'
      exclude group: 'junit'
    },
    hamcrest: "org.hamcrest:hamcrest:${testVersions.hamcrest}",
    jcacheGuice: "org.jsr107.ri:cache-annotations-ri-guice:${versions.jcache}",
    jcacheTck: "javax.cache:cache-tests:${testVersions.jcacheTck}",
    jcacheTckTests: "javax.cache:cache-tests:${testVersions.jcacheTck}:tests",
    jctools: "org.jctools:jctools-core:${testVersions.jctools}",
    junit: "junit:junit:${testVersions.junit}",
    mockito: "org.mockito:mockito-core:${testVersions.mockito}",
    osgiCompile: [
      'org.apache.felix:org.apache.felix.framework:6.0.3',
      "org.ops4j.pax.exam:pax-exam-junit4:${testVersions.paxExam}",
    ],
    osgiRuntime: [
      "org.ops4j.pax.exam:pax-exam-container-native:${testVersions.paxExam}",
      "org.ops4j.pax.exam:pax-exam-link-mvn:${testVersions.paxExam}",
      "org.ops4j.pax.url:pax-url-aether:2.6.1",
    ],
    testng: [
      dependencies.create("org.testng:testng:${testVersions.testng}") {
        exclude group: 'junit'
        exclude group: 'guice'
      },
      'com.google.inject:guice:4.2.2'
    ],
    truth: "com.google.truth:truth:${testVersions.truth}",
  ]
  gradlePlugins = [
    apt: "net.ltgt.gradle:gradle-apt-plugin:${pluginVersions.apt}",
    bnd: "biz.aQute.bnd:biz.aQute.bnd.gradle:${pluginVersions.bnd}",
    buildscan: "com.gradle:build-scan-plugin:${pluginVersions.buildscan}",
    checkstyle: dependencies.create("com.puppycrawl.tools:checkstyle:${pluginVersions.checkstyle}") {
      transitive = false
    },
    coveralls: "org.kt3k.gradle.plugin:coveralls-gradle-plugin:${pluginVersions.coveralls}",
    coverity: "gradle.plugin.com.github.mjdetullio.gradle:coverity-plugin:${pluginVersions.coverity}",
    errorprone: "net.ltgt.gradle:gradle-errorprone-plugin:${pluginVersions.errorprone}",
    jmh: [
      "me.champeau.gradle.jmh:me.champeau.gradle.jmh.gradle.plugin:${pluginVersions.jmh}",
      "org.openjdk.jmh:jmh-core:${versions.jmh}",
    ],
    jmhReport: "gradle.plugin.io.morethan.jmhreport:gradle-jmh-report:${pluginVersions.jmhReport}",
    nexus: "com.bmuschko:gradle-nexus-plugin:${pluginVersions.nexus}",
    nullaway: "net.ltgt.gradle:gradle-nullaway-plugin:${pluginVersions.nullaway}",
    semanticVersioning: "io.ehdev:gradle-semantic-versioning:${pluginVersions.semanticVersioning}",
    shadow: "com.github.jengelman.gradle.plugins:shadow:${pluginVersions.shadow}",
    sonarqube: "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${pluginVersions.sonarqube}",
    spotbugs: [
      "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:${pluginVersions.spotbugsPlugin}",
      libraries.guava // https://github.com/spotbugs/spotbugs-gradle-plugin/issues/119
    ],
    stats: "org.kordamp.gradle:stats-gradle-plugin:${pluginVersions.stats}",
    versions: "com.github.ben-manes:gradle-versions-plugin:${pluginVersions.versions}",
  ]
}
