/**
 * Defines dependencies and versions shared across modules.
 *
 * Multiple dependencies can be shared under the same name:
 *   spring: [
 *     "org.springframework:spring-core:${versions.spring}",
 *     "org.springframework:spring-jdbc:${versions.spring}"
 *   ]
 *
 * Use DependencyHandler.create for advanced dependency configuration:
 *   dependencies.create("org.springframework:spring-core:3.1") {
 *     // Force a version in case of the conflict
 *     force = true
 *
 *     // Disable all transitive dependencies
 *     transitive = false
 *
 *     // exclude a transitive dependency
 *     exclude module: "org.unwanted:unwantedModule" // by group & name
 *     exclude module: "unwantedModule"              // by artifact name
 *     exclude group:  "org.unwanted'                // by artifact group
 *   }
 *
 * @see http://gradle.org/docs/current/javadoc/org/gradle/api/artifacts/dsl/DependencyHandler.html
 */
ext {
  versions = [
    akka: '2.5.13',
    checkerFramework: '2.5.2',
    commonsCompress: '1.17',
    commonsLang3: '3.7',
    config: '1.3.3',
    errorProne: '2.3.1',
    fastutil: '8.2.1',
    flipTables: '1.0.2',
    guava: '25.1-jre',
    javapoet: '1.11.1',
    jcache: '1.1.0',
    jsr330: '1',
    univocityParsers: '2.6.4',
    ycsb: '0.13.0',
    xz: '1.8',
  ]
  testVersions = [
    awaitility: '3.1.0',
    easymock: '3.6',
    hamcrest: '2.0.0.0',
    jcacheTck: '1.1.0',
    jctools: '2.1.2',
    junit: '4.12',
    mockito: '2.19.0',
    paxExam: '4.11.0',
    testng: '6.14.3',
    truth: '0.24',
  ]
  benchmarkVersions = [
    cache2k: '1.0.2.Final',
    collision: '0.3.3',
    concurrentlinkedhashmap: '1.4.2',
    ehcache3: '3.5.2',
    elasticSearch: '6.3.0',
    expiringMap: '0.5.8',
    jackrabbit: '1.9.4',
    jamm: '0.3.2',
    javaObjectLayout: '0.9',
    jmh: '1.21',
    koloboke: '0.6.8',
    ohc: '0.6.1',
    slf4j: '1.7.25',
    tcache: '1.0.5',
  ]
  pluginVersions = [
    apt: '0.16',
    buildscan: '1.14',
    checkstyle: '8.10.1',
    coveralls: '2.8.2',
    coverity: '1.0.10',
    errorProne: '0.0.14',
    jacoco: '0.8.1',
    jmh: '0.4.6',
    jmhReport: '0.7.0',
    nexus: '2.3.1',
    pmd: '6.3.0',
    semanticVersioning: '1.1.0',
    shadow: '2.0.4',
    sonarqube: '2.6.2',
    spotbugs: '3.1.3',
    spotbugsPlugin: '1.6.2',
    stats: '0.2.2',
    versions: '0.20.0',
  ]
  annotationProcessorVersions = [
    nullaway: '0.4.7',
  ]

  libraries = [
    akka: "com.typesafe.akka:akka-actor_2.12:${versions.akka}",
    checkerAnnotations: "org.checkerframework:checker-qual:${versions.checkerFramework}",
    commonsCompress: "org.apache.commons:commons-compress:${versions.commonsCompress}",
    commonsLang3: "org.apache.commons:commons-lang3:${versions.commonsLang3}",
    config: "com.typesafe:config:${versions.config}",
    errorProneAnnotations: "com.google.errorprone:error_prone_annotations:${versions.errorProne}",
    errorProneCore: "com.google.errorprone:error_prone_core:${versions.errorProne}",
    fastutil: "it.unimi.dsi:fastutil:${versions.fastutil}",
    flipTables: "com.jakewharton.fliptables:fliptables:${versions.flipTables}",
    guava: "com.google.guava:guava:${versions.guava}",
    javapoet: "com.squareup:javapoet:${versions.javapoet}",
    jcache: "javax.cache:cache-api:${versions.jcache}",
    jsr330: "javax.inject:javax.inject:${versions.jsr330}",
    univocityParsers: "com.univocity:univocity-parsers:${versions.univocityParsers}",
    ycsb: "com.github.brianfrankcooper.ycsb:core:${versions.ycsb}",
    xz: "org.tukaani:xz:${versions.xz}",
  ]
  testLibraries = [
    awaitility: dependencies.create("org.awaitility:awaitility:${testVersions.awaitility}") {
      exclude group: 'org.hamcrest'
    },
    easymock: "org.easymock:easymock:${testVersions.easymock}",
    guavaTestLib: dependencies.create("com.google.guava:guava-testlib:${versions.guava}") {
      exclude group: 'com.google.truth'
      exclude group: 'junit'
    },
    hamcrest: "org.hamcrest:java-hamcrest:${testVersions.hamcrest}",
    jcacheGuice: "org.jsr107.ri:cache-annotations-ri-guice:${versions.jcache}",
    jcacheTck: "javax.cache:cache-tests:${testVersions.jcacheTck}",
    jcacheTckTests: "javax.cache:cache-tests:${testVersions.jcacheTck}:tests",
    jctools: "org.jctools:jctools-core:${testVersions.jctools}",
    junit: "junit:junit:${testVersions.junit}",
    mockito: dependencies.create("org.mockito:mockito-core:${testVersions.mockito}") {
      exclude group: 'org.hamcrest'
    },
    osgiCompile: [
      'org.apache.felix:org.apache.felix.framework:5.6.10',
      "org.ops4j.pax.exam:pax-exam-junit4:${testVersions.paxExam}",
    ],
    osgiRuntime: [
      "org.ops4j.pax.exam:pax-exam-container-native:${testVersions.paxExam}",
      "org.ops4j.pax.exam:pax-exam-link-mvn:${testVersions.paxExam}",
      "org.ops4j.pax.url:pax-url-aether:2.5.4",
    ],
    testng: [
      dependencies.create("org.testng:testng:${testVersions.testng}") {
        exclude group: 'junit'
        exclude group: 'guice'
      },
      'com.google.inject:guice:4.2.0'
    ],
    truth: "com.google.truth:truth:${testVersions.truth}",
  ]
  benchmarkLibraries = [
    cache2k: "org.cache2k:cache2k-core:${benchmarkVersions.cache2k}",
    collision: "systems.comodal:collision:${benchmarkVersions.collision}",
    concurrentlinkedhashmap: "com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:${benchmarkVersions.concurrentlinkedhashmap}",
    ehcache3: "org.ehcache:ehcache:${benchmarkVersions.ehcache3}",
    elasticSearch: dependencies.create("org.elasticsearch:elasticsearch:${benchmarkVersions.elasticSearch}") {
      exclude group: 'org.apache.lucene'
    },
    expiringMap: "net.jodah:expiringmap:${benchmarkVersions.expiringMap}",
    jackrabbit: dependencies.create("org.apache.jackrabbit:oak-core:${benchmarkVersions.jackrabbit}") {
      exclude group: 'junit'
    },
    jamm: "com.github.jbellis:jamm:${benchmarkVersions.jamm}",
    javaObjectLayout: "org.openjdk.jol:jol-cli:${benchmarkVersions.javaObjectLayout}",
    koloboke: [
      "net.openhft:koloboke-api-jdk8:${benchmarkVersions.koloboke}",
      "net.openhft:koloboke-impl-jdk8:${benchmarkVersions.koloboke}",
    ],
    ohc: "org.caffinitas.ohc:ohc-core-j8:${benchmarkVersions.ohc}",
    slf4jNop: "org.slf4j:slf4j-nop:${benchmarkVersions.slf4j}",
    tcache: "com.trivago:triava:${benchmarkVersions.tcache}",
  ]
  gradlePlugins = [
    apt: "net.ltgt.gradle:gradle-apt-plugin:${pluginVersions.apt}",
    buildscan: "com.gradle:build-scan-plugin:${pluginVersions.buildscan}",
    checkstyle: dependencies.create("com.puppycrawl.tools:checkstyle:${pluginVersions.checkstyle}") {
      transitive = false
    },
    coveralls: "org.kt3k.gradle.plugin:coveralls-gradle-plugin:${pluginVersions.coveralls}",
    coverity: "gradle.plugin.com.github.mjdetullio.gradle:coverity-plugin:${pluginVersions.coverity}",
    errorProne: "net.ltgt.errorprone:net.ltgt.errorprone.gradle.plugin:${pluginVersions.errorProne}",
    jmh: [
      "me.champeau.gradle.jmh:me.champeau.gradle.jmh.gradle.plugin:${pluginVersions.jmh}",
      "org.openjdk.jmh:jmh-core:${benchmarkVersions.jmh}",
    ],
    jmhReport: "gradle.plugin.io.morethan.jmhreport:gradle-jmh-report:${pluginVersions.jmhReport}",
    nexus: "com.bmuschko:gradle-nexus-plugin:${pluginVersions.nexus}",
    semanticVersioning: "io.ehdev:gradle-semantic-versioning:${pluginVersions.semanticVersioning}",
    shadow: "com.github.jengelman.gradle.plugins:shadow:${pluginVersions.shadow}",
    sonarqube: "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${pluginVersions.sonarqube}",
    spotbugs: "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:${pluginVersions.spotbugsPlugin}",
    stats: "org.kordamp.gradle:stats-gradle-plugin:${pluginVersions.stats}",
    versions: "com.github.ben-manes:gradle-versions-plugin:${pluginVersions.versions}",
  ]
  annotationProcessors = [
    nullaway: "com.uber.nullaway:nullaway:${annotationProcessorVersions.nullaway}",
  ]
}
