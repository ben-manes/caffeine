/**
 * Defines dependencies and versions shared across modules.
 *
 * Multiple dependencies can be shared under the same name:
 *   spring: [
 *     "org.springframework:spring-core:${versions.spring}",
 *     "org.springframework:spring-jdbc:${versions.spring}"
 *   ]
 *
 * Use DependencyHandler.create for advanced dependency configuration:
 *   dependencies.create("org.springframework:spring-core:3.1") {
 *     // Force a version in case of the conflict
 *     force = true
 *
 *     // Disable all transitive dependencies
 *     transitive = false
 *
 *     // exclude a transitive dependency
 *     exclude module: "org.unwanted:unwantedModule" // by group & name
 *     exclude module: "unwantedModule"              // by artifact name
 *     exclude group:  "org.unwanted'                // by artifact group
 *   }
 *
 * @see https://gradle.org/docs/current/javadoc/org/gradle/api/artifacts/dsl/DependencyHandler.html
 */
ext {
  versions = [
    akka: '2.6.13',
    cache2k: '2.1.2.Alpha',
    checkerFramework: '3.12.0',
    coherence: '20.06',
    commonsCompress: '1.20',
    commonsLang3: '3.12.0',
    commonsMath3: '3.6.1',
    concurrentlinkedhashmap: '1.4.2',
    config: '1.4.1',
    ehcache3: '3.9.2',
    errorprone: '2.6.0',
    errorproneJavac: '9+181-r4173-1',
    elasticSearch: '7.12.0',
    expiringMap: '0.5.9',
    fastfilter: '1.0',
    fastutil: '8.5.4',
    flipTables: '1.1.0',
    googleJavaFormat: '1.10.0',
    guava: '30.1.1-jre',
    jackrabbit: '1.38.0',
    jamm: '0.3.3',
    javaObjectLayout: '0.15',
    javapoet: '1.13.0',
    jcache: '1.1.1',
    jmh: '1.29',
    joor: '0.9.13',
    jsr330: '1',
    nullaway: '0.8.0',
    ohc: '0.6.1',
    osgiComponentAnnotations: '1.4.0',
    picocli: '4.6.1',
    slf4j: '1.7.30',
    tcache: '2.0.1',
    stream: '2.9.8',
    univocityParsers: '2.9.1',
    ycsb: '0.17.0',
    xz: '1.9',
    zstd: '1.4.9-3',
  ]
  testVersions = [
    awaitility: '4.0.3',
    easymock: '4.2',
    guice: '5.0.1',
    hamcrest: '2.2',
    jcacheTck: '1.1.1',
    jctools: '3.3.0',
    junit: '4.13.2',
    mockito: '3.8.0',
    paxExam: '4.13.4',
    testng: '7.4.0',
    truth: '1.1.2',
    felix: '7.0.0',
    felixScr: '2.1.26',
    osgiUtilFunction: '1.1.0',
    osgiUtilPromise: '1.1.1',
  ]
  pluginVersions = [
    bnd: '5.3.0',
    checkerFramework: '0.5.18',
    checkstyle: '8.41.1',
    coveralls: '2.8.4',
    errorprone: '2.0.1',
    jacoco: '0.8.6',
    jmh: '0.5.3',
    jmhReport: '0.9.0',
    nexusPublish: '1.0.0',
    nullaway: '1.1.0',
    pmd: '6.33.0',
    semanticVersioning: '1.1.0',
    shadow: '6.1.0',
    sonarqube: '3.1.1',
    spotbugs: '4.2.2',
    spotbugsPlugin: '4.7.0',
    stats: '0.2.2',
    versions: '0.38.0',
  ]
  annotationProcessorVersions = [
    autoValue: '1.7.5',
    autoValueBuilder: '2.9.3',
  ]

  libraries = [
    akka: "com.typesafe.akka:akka-actor_2.12:${versions.akka}",
    cache2k: "org.cache2k:cache2k-core:${versions.cache2k}",
    checkerAnnotations: "org.checkerframework:checker-qual:${versions.checkerFramework}",
    checkerFramework: "org.checkerframework:checker:${versions.checkerFramework}",
    commonsCompress: "org.apache.commons:commons-compress:${versions.commonsCompress}",
    commonsLang3: "org.apache.commons:commons-lang3:${versions.commonsLang3}",
    commonsMath3: "org.apache.commons:commons-math3:${versions.commonsMath3}",
    concurrentlinkedhashmap: "com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:${versions.concurrentlinkedhashmap}",
    config: "com.typesafe:config:${versions.config}",
    ehcache3: "org.ehcache:ehcache:${versions.ehcache3}",
    errorproneAnnotations: "com.google.errorprone:error_prone_annotations:${versions.errorprone}",
    errorproneCore: "com.google.errorprone:error_prone_core:${versions.errorprone}",
    errorproneJavac: "com.google.errorprone:javac:${versions.errorproneJavac}",
    elasticSearch: dependencies.create("org.elasticsearch:elasticsearch:${versions.elasticSearch}") {
      exclude module: 'elasticsearch-plugin-classloader'
      exclude group: 'org.apache.lucene'
    },
    expiringMap: "net.jodah:expiringmap:${versions.expiringMap}",
    fastfilter: "com.github.FastFilter:fastfilter_java:${versions.fastfilter}",
    fastutil: "it.unimi.dsi:fastutil:${versions.fastutil}",
    flipTables: "com.jakewharton.fliptables:fliptables:${versions.flipTables}",
    googleJavaFormat: "com.google.googlejavaformat:google-java-format:${versions.googleJavaFormat}",
    guava: "com.google.guava:guava:${versions.guava}",
    jackrabbit: dependencies.create("org.apache.jackrabbit:oak-core:${versions.jackrabbit}") {
      exclude group: 'junit'
    },
    jamm: "com.github.jbellis:jamm:${versions.jamm}",
    javaObjectLayout: "org.openjdk.jol:jol-cli:${versions.javaObjectLayout}",
    javapoet: "com.squareup:javapoet:${versions.javapoet}",
    jcache: "javax.cache:cache-api:${versions.jcache}",
    jsr330: "javax.inject:javax.inject:${versions.jsr330}",
    joor: "org.jooq:joor-java-8:${versions.joor}",
    nullaway: "com.uber.nullaway:nullaway:${versions.nullaway}",
    ohc: "org.caffinitas.ohc:ohc-core-j8:${versions.ohc}",
    osgiComponentAnnotations: "org.osgi:org.osgi.service.component.annotations:${versions.osgiComponentAnnotations}",
    picocli: "info.picocli:picocli:${versions.picocli}",
    slf4jNop: "org.slf4j:slf4j-nop:${versions.slf4j}",
    tcache: "com.trivago:triava:${versions.tcache}",
    stream: "com.clearspring.analytics:stream:${versions.stream}",
    univocityParsers: "com.univocity:univocity-parsers:${versions.univocityParsers}",
    ycsb: "site.ycsb:core:${versions.ycsb}",
    xz: "org.tukaani:xz:${versions.xz}",
    zstd: "com.github.luben:zstd-jni:${versions.zstd}",
  ]
  testLibraries = [
    awaitility: "org.awaitility:awaitility:${testVersions.awaitility}",
    easymock: "org.easymock:easymock:${testVersions.easymock}",
    guavaTestLib: dependencies.create("com.google.guava:guava-testlib:${versions.guava}") {
      exclude group: 'com.google.truth'
      exclude group: 'junit'
    },
    hamcrest: "org.hamcrest:hamcrest:${testVersions.hamcrest}",
    jcacheGuice: "org.jsr107.ri:cache-annotations-ri-guice:${versions.jcache}",
    jcacheTck: "javax.cache:cache-tests:${testVersions.jcacheTck}",
    jcacheTckTests: "javax.cache:cache-tests:${testVersions.jcacheTck}:tests",
    jctools: "org.jctools:jctools-core:${testVersions.jctools}",
    junit: "junit:junit:${testVersions.junit}",
    mockito: "org.mockito:mockito-core:${testVersions.mockito}",
    osgiCompile: [
      "org.ops4j.pax.exam:pax-exam-junit4:${testVersions.paxExam}",
    ],
    osgiRuntime: [
      "org.apache.felix:org.apache.felix.framework:${testVersions.felix}",
      "org.apache.felix:org.apache.felix.scr:${testVersions.felixScr}",
      "org.osgi:org.osgi.util.function:${testVersions.osgiUtilFunction}",
      "org.osgi:org.osgi.util.promise:${testVersions.osgiUtilPromise}",
      "org.ops4j.pax.exam:pax-exam-container-native:${testVersions.paxExam}",
      "org.ops4j.pax.exam:pax-exam-link-mvn:${testVersions.paxExam}",
      "org.ops4j.pax.url:pax-url-aether:2.6.5",
    ],
    testng: [
      dependencies.create("org.testng:testng:${testVersions.testng}") {
        exclude group: 'junit'
        exclude group: 'guice'
      },
      "com.google.inject:guice:${testVersions.guice}"
    ],
    truth: "com.google.truth:truth:${testVersions.truth}",
  ]
  gradlePlugins = [
    bnd: "biz.aQute.bnd:biz.aQute.bnd.gradle:${pluginVersions.bnd}",
    checkerFramework: "org.checkerframework:checkerframework-gradle-plugin:${pluginVersions.checkerFramework}",
    checkstyle: dependencies.create("com.puppycrawl.tools:checkstyle:${pluginVersions.checkstyle}") {
      transitive = false
    },
    coveralls: "gradle.plugin.com.github.kt3k.coveralls:coveralls-gradle-plugin:${pluginVersions.coveralls}",
    errorprone: "net.ltgt.gradle:gradle-errorprone-plugin:${pluginVersions.errorprone}",
    jmh: [
      "me.champeau.gradle.jmh:me.champeau.gradle.jmh.gradle.plugin:${pluginVersions.jmh}",
      "org.openjdk.jmh:jmh-core:${versions.jmh}",
    ],
    jmhReport: "gradle.plugin.io.morethan.jmhreport:gradle-jmh-report:${pluginVersions.jmhReport}",
    nexusPublish: "io.github.gradle-nexus:publish-plugin:${pluginVersions.nexusPublish}",
    nullaway: "net.ltgt.gradle:gradle-nullaway-plugin:${pluginVersions.nullaway}",
    semanticVersioning: "io.ehdev:gradle-semantic-versioning:${pluginVersions.semanticVersioning}",
    shadow: "com.github.jengelman.gradle.plugins:shadow:${pluginVersions.shadow}",
    sonarqube: "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${pluginVersions.sonarqube}",
    spotbugs: "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:${pluginVersions.spotbugsPlugin}",
    stats: "org.kordamp.gradle:stats-gradle-plugin:${pluginVersions.stats}",
    versions: "com.github.ben-manes:gradle-versions-plugin:${pluginVersions.versions}",
  ]
  annotationProcessors = [
    autoValue: [
      "com.google.auto.value:auto-value:${annotationProcessorVersions.autoValue}",
      "com.github.h908714124:auto-builder:${annotationProcessorVersions.autoValueBuilder}",
      "com.google.auto.value:auto-value-annotations:${annotationProcessorVersions.autoValue}",
    ],
  ]
}
