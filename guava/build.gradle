/**
 * Guava compatibility adapter.
 *
 * The tests are forked from Guava commit e370dde.
 */
dependencies {
  compile project(':caffeine')
  compile libraries.guava

  testCompile testLibraries.junit
  testCompile testLibraries.truth
  testCompile testLibraries.jctools
  testCompile testLibraries.easymock
  testCompile testLibraries.guavaTestLib
}

jar.manifest {
  name 'com.github.ben-manes.caffeine.guava'
  instruction 'Import-Package',
    'com.google.common.cache',
    'com.github.benmanes.caffeine.cache',
    'com.github.benmanes.caffeine.cache.stats'
  instruction 'Export-Package',
    'com.github.benmanes.caffeine.guava'
  instruction 'Automatic-Module-Name',
    'com.github.benmanes.caffeine.guava'
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
}

test {
  useJUnit {
    excludeCategories 'com.github.benmanes.caffeine.guava.OSGiTests'
  }
}

task osgiTests(type: Test, group: 'Cache tests', description: 'Isolated OSGi tests') {
  enabled = !JavaVersion.current().isJava9Compatible()
  tasks.test.dependsOn(it)

  useJUnit {
    includeCategories 'com.github.benmanes.caffeine.guava.OSGiTests'
  }
}

tasks.withType(Test) {
  systemProperty 'guava.osgi.version', versions.guava
  systemProperty 'caffeine.osgi.jar', project(':caffeine').jar.archivePath.path
  systemProperty 'caffeine-guava.osgi.jar', project(':guava').jar.archivePath.path
}
